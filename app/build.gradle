apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-parcelize'
apply plugin: 'com.hiya.jacoco-android'
apply plugin: "com.starter.easylauncher"


Properties keystoreProperties = new Properties()
def keystorePropertiesFile = new File('../keystore.properties')

if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
} else{
    println "--> keystore not exists"
}

android {
    compileSdkVersion 33
    packagingOptions {
        resources {
            excludes += ['proguard-project.txt', 'project.properties', 'META-INF/LICENSE.txt', 'META-INF/LICENSE', 'META-INF/NOTICE.txt', 'META-INF/NOTICE', 'META-INF/DEPENDENCIES.txt', 'META-INF/DEPENDENCIES']
        }
    }


    defaultConfig {
        applicationId "com.mobilestartools.filemanager"
        minSdkVersion 19
        targetSdkVersion 33
        versionCode 118
        versionName "3.8.5"
        multiDexEnabled true

        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation"  : "$projectDir/src/test/resources/schemas".toString(),
                              "room.incremental"     : "true",
                              "room.expandProjection": "true"]
            }
        }

        setProperty("archivesBaseName", "${rootProject.name}-${versionName}")

    }


    applicationVariants.all { variant ->
        if (variant.getBuildType().isMinifyEnabled()) {
            variant.assemble.doLast {
                copy {
                    from variant.mappingFile
                    into "${projectDir}/release"
                    rename { String fileName ->
                        "mapping-${versionName}.txt"
                    }
                }

                variant.outputs.each { output->
                    def apkProcess = output.getOutputFile();
                    copy {
                        from "$apkProcess"
                        into "${projectDir}/release"
                        // rename ("$apkProcess.name", "$apkProcessNewName")
                    }
                }
            }
        }

        if (variant.getBuildType().name == "release"){
            variant.outputs.all { output ->
                def apkName = "Star-File-" + android.defaultConfig.versionName +"-release.apk"
                outputFileName = apkName
            }
        }

    }

    signingConfigs {
        if (keystorePropertiesFile.exists()) {
            register("release") {
                keyAlias = keystoreProperties.getProperty("keyAlias")
                keyPassword = keystoreProperties.getProperty("keyPassword")
                storeFile = file(keystoreProperties.getProperty("storeFile"))
                storePassword = keystoreProperties.getProperty("storePassword")
            }
        }
        config
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.cfg'
            testProguardFile 'tests-proguard.cfg'
            buildConfigField "String", "CRYPTO_IV", "\"LxbHiJhhUXcj\""
            buildConfigField "String", "FTP_SERVER_KEYSTORE_PASSWORD", "\"vishal007\""
            debuggable true //For "debug" banner on icon
        }

        release {
            if (keystorePropertiesFile.exists()) {
                signingConfig = signingConfigs.getByName("release")
            }

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.cfg'
            buildConfigField "String", "CRYPTO_IV", "\"LxbHiJhhUXcj\""
            buildConfigField "String", "FTP_SERVER_KEYSTORE_PASSWORD", "\"vishal007\""
        }
    }

    buildFeatures {
        viewBinding true
    }


    flavorDimensions 'static'

    productFlavors {
        fdroid {
            dimension 'static'
            buildConfigField "boolean", "IS_VERSION_FDROID", "true"

        }

        play {
            dimension 'static'
            buildConfigField "boolean", "IS_VERSION_FDROID", "false"
        }
    }


    sourceSets {
        test.java.srcDirs += '../testShared/src/test/java'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = '11'
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }
    lint {
        abortOnError false
    }
    namespace 'com.amaze.filemanager'
}

dependencies {

    modules {
        module("org.bouncycastle:bcprov-jdk15to18") {
            replacedBy("org.bouncycastle:bcprov-jdk15on")
        }
        module("org.bouncycastle:bcpkix-jdk15to18") {
            replacedBy("org.bouncycastle:bcpkix-jdk15on")
        }
    }

    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.readystatesoftware.systembartint:systembartint:1.0.3'

    implementation 'androidx.vectordrawable:vectordrawable-animated:1.1.0'
    implementation 'androidx.legacy:legacy-support-v13:1.0.0'
    implementation "com.google.android.material:material:$androidMaterialVersion"
    implementation "androidx.fragment:fragment-ktx:$androidXFragmentVersion"
    implementation "androidx.appcompat:appcompat:$androidXAppCompatVersion"
    implementation 'androidx.palette:palette-ktx:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation "androidx.room:room-runtime:$roomVersion"
    playImplementation "com.android.billingclient:billing:$androidBillingVersion"
    kapt "androidx.room:room-compiler:$roomVersion"
    // RxJava support for Room
    implementation "androidx.room:room-rxjava2:$roomVersion"
    kapt "androidx.annotation:annotation:$androidXAnnotationVersion"

    implementation "androidx.preference:preference-ktx:$androidXPrefVersion"

    //For tests
    testImplementation "junit:junit:$junitVersion"//tests the app logic
    testImplementation "org.robolectric:robolectric:$robolectricVersion"//tests android interaction
    testImplementation "org.robolectric:shadows-httpclient:$robolectricVersion"//tests android interaction
    testImplementation "androidx.test:core:$androidXTestVersion"
    testImplementation "androidx.test:runner:$androidXTestVersion"
    testImplementation "androidx.test:rules:$androidXTestVersion"
    testImplementation "androidx.test.ext:junit:$androidXTestExtVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "org.mockito:mockito-inline:$mockitoInlineVersion"
    testImplementation "org.mockito.kotlin:mockito-kotlin:$mockitoKotlinVersion"
    testImplementation "org.apache.sshd:sshd-core:1.7.0"
    testImplementation "org.awaitility:awaitility:$awaitilityVersion"
    testImplementation "org.jsoup:jsoup:$jsoupVersion"
    testImplementation "androidx.room:room-migration:$roomVersion"
    testImplementation "io.mockk:mockk:$mockkVersion"
    kaptTest "com.google.auto.service:auto-service:1.0-rc4"

    androidTestImplementation "junit:junit:$junitVersion"//tests the app logic
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation "androidx.test:core:$androidXTestVersion"
    androidTestImplementation "androidx.test:runner:$androidXTestVersion"
    androidTestImplementation "androidx.test:rules:$androidXTestVersion"
    androidTestImplementation "androidx.test.ext:junit:$androidXTestExtVersion"
    // androidTestImplementation "androidx.test.uiautomator:uiautomator:$uiAutomatorVersion"
    androidTestImplementation "commons-net:commons-net:$commonsNetVersion"
    androidTestImplementation "org.awaitility:awaitility:$awaitilityVersion"

    //Detect memory leaks
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.7'

    implementation "org.apache.commons:commons-compress:$commonsCompressVersion"

    playImplementation "com.github.junrar:junrar:$junrarVersion"

    implementation "com.afollestad.material-dialogs:core:$materialDialogsVersion"
    implementation "com.afollestad.material-dialogs:commons:$materialDialogsVersion"

    // https://mvnrepository.com/artifact/org.apache.mina/mina-core
    implementation group: 'org.apache.mina', name: 'mina-core', version: '2.0.16'

    //implementation files('libs/ftplet-api-1.1.0-SNAPSHOT.jar')
    // https://mvnrepository.com/artifact/org.apache.ftpserver/ftplet-api
    implementation "org.apache.ftpserver:ftplet-api:$ftpserverVersion"

    //implementation files('libs/ftpserver-core-1.1.0-SNAPSHOT.jar')
    // https://mvnrepository.com/artifact/org.apache.ftpserver/ftpserver-core
    implementation "org.apache.ftpserver:ftpserver-core:$ftpserverVersion"

    implementation 'org.greenrobot:eventbus:3.3.1'

    implementation "com.github.topjohnwu.libsu:core:${libsuVersion}"
    implementation "com.github.topjohnwu.libsu:io:${libsuVersion}"

    playImplementation 'com.cloudrail:cloudrail-si-android:2.22.4'

    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.2'//Nice charts and graphs

    implementation 'com.github.npgall:concurrent-trees:2.6.1'//Concurrent tries

    //SFTP
    implementation "com.hierynomus:sshj:$sshjVersion"

    //smb
    implementation "eu.agno3.jcifs:jcifs-ng:$jcifsVersion"

    //FTP
    implementation "commons-net:commons-net:$commonsNetVersion"

    //OkHttp
    implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"

    implementation "org.bouncycastle:bcpkix-jdk15on:$bouncyCastleVersion"
    implementation "org.bouncycastle:bcprov-jdk15on:$bouncyCastleVersion"

    //Glide: loads icons seemlessly
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    implementation ("com.github.bumptech.glide:recyclerview-integration:$glideVersion") {
        // Excludes the support library because it's already included by Glide.
        transitive = false
    }
    kapt "com.github.bumptech.glide:compiler:$glideVersion"

    implementation "com.leinardi.android:speed-dial:$fabSpeedDialVersion"

    //Simple library show
    implementation('com.mikepenz:aboutlibraries:6.1.1@aar') {
        transitive = true
    }

    implementation 'androidx.multidex:multidex:2.0.1'//Multiple dex files
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    //zip4j: support password-protected zips
    implementation "net.lingala.zip4j:zip4j:$zip4jVersion"

    implementation 'org.tukaani:xz:1.9'

    implementation "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"
    // Because RxAndroid releases are few and far between, it is recommended you also
    // explicitly depend on RxJava's latest version for bug fixes and new features.
    // (see https://github.com/ReactiveX/RxJava/releases for latest 3.x.x version)
    implementation "io.reactivex.rxjava2:rxjava:$rxJavaVersion"

    implementation project(':commons_compress_7z')
    implementation project(':file_operations')
    implementation project(':portscanner')
    implementation "androidx.core:core-ktx:$androidXCoreVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "ch.acra:acra-core:5.7.0"
    runtimeOnly 'org.slf4j:slf4j-api:1.7.36'
    runtimeOnly "com.github.tony19:logback-android:$logbackAndroidVersion"

    implementation 'com.google.code.gson:gson:2.9.1'

}

kotlin {
    jvmToolchain(11)
}

configurations.all {
    resolutionStrategy {
        dependencySubstitution {
            substitute module("commons-logging:commons-logging-api:1.1") using module("commons-logging:commons-logging:1.1.1")
            substitute module("com.android.support:support-annotations:27.1.1") using module("com.android.support:support-annotations:27.0.2")
            // These two lines are added to prevent possible class clashes between awaitility (which uses hamcrest 2.1) and junit (which uses hamcrest 1.3).
            substitute module('org.hamcrest:hamcrest-core:1.3') using module("org.hamcrest:hamcrest:2.1")
            substitute module('org.hamcrest:hamcrest-library:1.3') using module("org.hamcrest:hamcrest:2.1")
        }
    }
}

configurations.all { config ->
    // This workarounds logback's multiple bindings problem when running tests.
    if (config.name.toLowerCase().contains('test')) {
        config.resolutionStrategy.dependencySubstitution {
            substitute module("com.github.tony19:logback-android:$logbackAndroidVersion") with module("ch.qos.logback:logback-classic:1.2.9")
        }
    }
}

task supportOldLangCodes

[['id', 'in'], ['yi', 'ji'], ['he', 'iw']].forEach { sourceCode, destinationCode ->
    def copyTask = tasks.create('copyStrings' + sourceCode + 'Into' + destinationCode, Copy) {
        description('copying values-' + sourceCode + ' strings into values-' + destinationCode)
        from('src/main/res/values-' + sourceCode)
        into('src/main/res/values-' + destinationCode)
        include('strings.xml')
    }
    supportOldLangCodes.dependsOn copyTask
}

project.afterEvaluate {
    preBuild.dependsOn supportOldLangCodes
}

clean.dependsOn supportOldLangCodes
clean.mustRunAfter supportOldLangCodes

jacoco {
    toolVersion = "${jacocoVersion}"
}

jacocoAndroidUnitTestReport {
    html.enabled true
    xml.enabled true
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
    jacoco.excludes = ['jdk.internal.*']
}

repositories {
    mavenCentral()
}
